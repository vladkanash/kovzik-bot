buildscript {
    ext.kotlin_version = '1.5.20'
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
}

group 'org.vladkanash'
version '0.1.0'

repositories {
    mavenCentral()
}

configurations {
    invoker
}

dependencies {
    runtimeOnly project(':publisher')
    invoker 'com.google.cloud.functions.invoker:java-function-invoker:1.0.0-alpha-2-rc5'
}

subprojects {
    apply plugin: "org.jetbrains.kotlin.jvm"

    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.2.1"
        implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2"
    }
}

task runFunction(type: JavaExec) {
    main = 'com.google.cloud.functions.invoker.runner.Invoker'
    classpath = configurations.invoker
    inputs.files(configurations.runtimeClasspath, sourceSets.main.output)
    args(
            '--target', project.findProperty('run.functionTarget') ?: '',
            '--port', project.findProperty('run.port') ?: 8080
    )
    doFirst {
        args('--classpath', files(configurations.runtimeClasspath, sourceSets.main.output).asPath)
    }
}

task deployFunction(type: Exec) {
    workingDir './'
    commandLine 'gcloud', 'functions', "kovzik-bot-publisher" , 'deploy', '--entry-point', 'org.vladkanash.publisher.Publisher'
}
